(mod (MOD TIC_TAC_TOE_PUZZLE CURRIED_TIC_TAC_TOE_PUZZLE odd_amount position next_v)
    (include condition_codes.clib)

    (defun sha256tree (TREE)
      (if (l TREE)
          (sha256 2 (sha256tree (f TREE)) (sha256tree (r TREE)))
          (sha256 1 TREE)
      )
    )

    ;;https://chialisp.com/docs/common_functions#currying
    (defun fix_curry_args (items core)
        (if items
            (qq (c (q . (unquote (f items))) (unquote (fix_curry_args (r items) core))))
            core
        )
    )

    (defun curry (func list_of_args) 
        (qq (a (q . (unquote func)) (unquote (fix_curry_args list_of_args (q . 1)))))
    )

    (defun create-coinditions (puzzle_hash odd_amount)
        (list
            (list ASSERT_MY_AMOUNT odd_amount)
            (list CREATE_COIN puzzle_hash odd_amount)
        )
    )

    (defun check-if-winning (MOD TIC_TAC_TOE_PUZZLE odd_amount next_v play_result)
        (if (= 1 (f play_result))
            (list
                (list CREATE_COIN 0x900ddeed -113)
            )
            (create-coinditions
                ; puzzle_hash
                (sha256tree
                    (curry
                        MOD
                        (list
                            MOD
                            TIC_TAC_TOE_PUZZLE
                            (curry
                                TIC_TAC_TOE_PUZZLE 
                                (list 
                                    (f (r play_result)) 
                                    next_v
                                )
                            )
                        )
                    )
                )
                odd_amount
            )
        )
    )

    (check-if-winning
        MOD 
        TIC_TAC_TOE_PUZZLE
        odd_amount
        next_v
        (a CURRIED_TIC_TAC_TOE_PUZZLE (list position))
    )
)
